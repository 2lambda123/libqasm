# bnf.gr
# This bnf for qasm assumes that the inputs have all been converted to lower case (This is to replicate case insensitivity)
> <qasm-file>



# Describe the general structure of a qasm file
<qasm-file> ::= <qubit-register> <line-separator> <circuits>

<circuits> ::= <circuit> | <circuit> <circuits>
<circuit> ::= <subcircuit> <statements> | <statements>

<subcircuit> ::= "." <identifier> <newline> | "." <identifier> "(" <number> ")" <newline>

<statements> ::= <qasm-line> <newline> | 
                 <qasm-line> <newline> <statements>
<qasm-line> ::= <map-operation> | <measure-parity-operation> | <measureall-operation> |
                <regular-operations> | <binary-controlled-operations> | <parallel-operations> |
                <special-operations>



# We define the convenience strings, texts, numbers here....
<newline> ::= "\n"
<line-separator> ::= <newline> | <newline> <line-separator>

<number> ::= <non-zero-digit> | <non-zero-digit> <digits>
<number-with-0> ::= <digit> | <digit> <digits>
<real-number> ::= <number> | <number> "." <number> | "." <number> | <number-with-0> "." <number>
<float> ::= <real-number> | <real-number> "e" <number-with-0>
<digits> ::= <digit> | <digit> <digits>
<digit> ::= "0" | <non-zero-digit>
<non-zero-digit> ::= "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"

<letter> ::= "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" |
             "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z"

<symbol> ::= <letter> | <digit>

<identifier> ::= <letter> | <letter> <identifier-tail>
<identifier-tail> ::= <symbol> | <symbol> <identifier-tail>

<indices> ::= "[" <numerical-identifier-list> "]" | "[" <numerical-identifier-range> "]"

<numerical-identifier-list> ::= <number> | <number> "," <numerical-identifier-list>
<numerical-identifier-range> ::= <number> ":" <number>

<axis> ::= "x" | "y" | "z"



# This defines the number of qubits taking part in this qasm file
<qubit-register> ::= "qubits " <number>



# We define the syntax for selecting the qubits/bits, either by a range or a list
<qubit-nomap> ::= "q[" <number> "]"
<qubit-selection> ::= "q" <indices> | <identifier>

## Next we define a classical bit, which is required to perform control gate operations
<bit-nomap> ::= "b[" <number> "]"
<bit-selection> ::= "b" <indices> | <identifier>


# Define the single qubit operation line
<single-qubit-operation> ::= <single-qubit-gate> " " <qubit-selection> | <prep_measure-ops> " " <qubit-nomap>
<single-qubit-operation-args> ::= <single-qubit-gate-args> " " <qubit-selection> ", " <float>
<map-operation> ::= "map" " " <qubit-selection> "," <identifier> | "map" " " <bit-selection> "," <identifier>

## Define the single qubit operations/gates
<single-qubit-gate> ::= "i" | "h" | "x" | "y" | "z" | "x90" | "y90" | 
                        "mx90" | "my90" | "s" | "sdag" | "t" | "tdag"
<single-qubit-gate-args> ::= <rotations>

## The gates definitions
<rotations> ::= "r" <axis>



# This is to define the state preparation/measurement
<prep_measure-ops> ::= <prep> | <measure>
<prep> ::= "prep" | "prep_" <axis>
<measure> ::= "measure" | "measure_" <axis>
<measure-parity-operation> ::= <measureparity> " " <qubit-nomap> "," <axis> | <measure-parity-operation> "," <qubit-nomap> "," <axis>
<measureparity> ::= "measure_parity"
<measureall-operation> ::= "measure_all"



# Qubit-controlled operations
<two-qubit-operation> ::= <two-qubit-gates> " " <qubit-selection> "," <qubit-selection>
<two-qubit-operation-args> ::= <two-qubit-gate-args> " " <qubit-selection> "," <qubit-selection> ", " <number>

## Define the two qubit gates
<two-qubit-gates> ::= "cnot" | "cz" | "swap"
<two-qubit-gate-args> ::= "cr"

## Define the toffoli gate
<toffoli-operation> ::= <toffoli-gate> " " <qubit-selection> "," <qubit-selection> "," <qubit-selection>
<toffoli-gate> ::= "toffoli"

## Define a superset of all general "normal" operations
<regular-operations> ::= <single-qubit-operation> | <single-qubit-operation-args> |
                         <two-qubit-operation> | <two-qubit-operation-args> | <toffoli-operation>


# Binary-controlled operations
<qubit-gates> ::= <single-qubit-gate> | <single-qubit-gate-args> | <rotations> | <two-qubit-gates> | <two-qubit-gate-args> | <toffoli-gate>

<binary-controlled-operations> ::= <bit-single-qubit-operation> | <bit-single-qubit-operation-args> |
                                   <bit-two-qubit-operation> | <bit-two-qubit-operation-args> | <bit-toffoli-operation> |
                                   <negate-binary-operation>

<bit-single-qubit-operation> ::= "c-" <single-qubit-gate> " " <bit-selection> "," <qubit-selection>
<bit-single-qubit-operation-args> ::= "c-" <single-qubit-gate-args> " " <bit-selection> "," <qubit-selection> ", " <float>

<bit-two-qubit-operation> ::= "c-" <two-qubit-gates> " " <bit-selection> "," <qubit-selection> "," <qubit-selection>
<bit-two-qubit-operation-args> ::= "c-" <two-qubit-gate-args> " " <bit-selection> "," <qubit-selection> "," <qubit-selection> ", " <number>

<bit-toffoli-operation> ::= "c-" <toffoli-gate> " " <bit-selection> "," <qubit-selection> "," <qubit-selection> "," <qubit-selection>

<negate-binary-operation> ::= "not " <bit-selection>



# Parallel Quantum Gates -> parallelizable-ops have a minimum of two operations
<parallel-operations> ::= "{" <parallelizable-ops> "}"
<all-valid-operations> ::= <regular-operations> | <binary-controlled-operations> 
<parallelizable-ops> ::= <all-valid-operations> " | " <all-valid-operations> | <all-valid-operations> " | " <parallelizable-ops>



# Special operations
<special-operations> ::= <display-operation> | <wait-operation> | <reset-averaging-operation>
<display-operation> ::= "display" | "display" " " <bit-selection>
<wait-operation> ::= "wait" " " <number>
<reset-averaging-operation> ::= "reset-averaging" | "reset-averaging" " " <qubit-selection>
