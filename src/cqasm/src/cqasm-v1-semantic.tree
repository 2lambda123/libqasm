# Implementation for the semantic tree node classes.
source

# Header file for the semantic tree node classes.
header

// Include tree base classes.
include "cqasm-tree.hpp"
tree_namespace cqasm::tree

// Include primitive types.
include "cqasm-v1-primitives.hpp"
include "cqasm-v1-values.hpp"
include "cqasm-v1-error-model.hpp"
include "cqasm-v1-instruction.hpp"
import cqasm.v1.primitives
import cqasm.v1.values
import cqasm.v1.types
import cqasm.v1.error_model
import cqasm.v1.instruction

// Initialization function to use to construct default values for the tree base
// classes and primitives.
initialize_function cqasm::v1::primitives::initialize
serdes_functions cqasm::v1::primitives::serialize cqasm::v1::primitives::deserialize

// Include SourceLocation annotation object for the debug dump generator.
src_include "cqasm-annotations.hpp"
location cqasm::annotations::SourceLocation

# Namespace for the semantic tree node classes.
namespace cqasm
namespace v1
namespace semantic

# Represents an annotation.
annotation_data {

    # The interface this annotation is intended for. If a target doesn't
    # support an interface, it should silently ignore the annotation.
    interface: cqasm::v1::primitives::Str;

    # The operation within the interface that this annotation is intended for.
    # If a supports the corresponding interface but not the operation, it
    # should throw an error.
    operation: cqasm::v1::primitives::Str;

    # Any operands attached to the annotation.
    operands: external Any<cqasm::v1::values::Node>;

}

# Represents a node that carries annotation data.
annotated {

    # Zero or more annotations attached to this object.
    annotations: Any<annotation_data>;

    # Error model information.
    error_model {

        # Error model type as registered through the API.
        model: cqasm::v1::error_model::ErrorModelRef;

        # Name as it appears in the cQASM file.
        name: cqasm::v1::primitives::Str;

        # Error model parameters.
        parameters: external Any<cqasm::v1::values::Node>;

    }

    # An instruction (a.k.a. gate).
    instruction {

        # Instruction type as registered through the API.
        instruction: cqasm::v1::instruction::InstructionRef;

        # Name as it appears in the cQASM file.
        name: cqasm::v1::primitives::Str;

        # Condition (c- notation). When there is no condition, this is a
        # constant boolean set to true.
        condition: external One<cqasm::v1::values::Node>;

        # Operands for the instruction.
        operands: external Any<cqasm::v1::values::Node>;

    }

    # A bundle of instructions, to be executed in parallel.
    bundle {

        # The list of parallel instructions.
        items: Many<instruction>;

    }

    # A subcircuit. That is, a named collection of bundles, possibly repeated a
    # number of times.
    subcircuit {

        # The name of the subcircuit. If the file doesn't start with a subcircuit
        # definition, this is empty for the first subcircuit.
        name: cqasm::v1::primitives::Str;

        # An optional integer expression representing the number of
        # iterations for this subcircuit. This is 1 when the iteration count is
        # not specified.
        iterations: cqasm::v1::primitives::Int;

        # The instruction bundles contained by this subcircuit.
        bundles: Any<bundle>;

    }

    # A mapping. That is, a user-defined identifier mapping to some value.
    mapping {

        # The name of the mapping.
        name: cqasm::v1::primitives::Str;

        # The value it maps to.
        value: external One<cqasm::v1::values::Node>;

    }

    # A variable.
    variable {

        # The name of the variable.
        name: cqasm::v1::primitives::Str;

        # The type of the variable.
        typ: external One<cqasm::v1::types::Node>;

    }

}

# The file version identifier.
version {

    # The list of version components, ordered major to minor.
    items: cqasm::v1::primitives::Version;

}

# A complete program.
program {

    # File version.
    version: One<version>;

    # The required qubit register size.
    num_qubits: cqasm::v1::primitives::Int;

    # Error model information.
    error_model: Maybe<error_model>;

    # The list of subcircuit.
    subcircuits: Any<subcircuit>;

    # The list of all user-defined mappings after parsing.
    mappings: Any<mapping>;

    # This list of all user-defined variables at any point in the code.
    variables: Any<variable>;

}
